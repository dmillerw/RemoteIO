buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.1-SNAPSHOT'
    }
}

apply plugin: 'forge'

version = "0.1.5.5"
group = "com.edxmod"
archivesBaseName = "Electrodynamics"
ext.modId = "EDX"

gradle.taskGraph.whenReady { taskGraph ->
    if (!taskGraph.hasTask(release)) {
        version += '-SNAPSHOT'
    }
}

task release(dependsOn: 'build') << {
    println 'Release deploy!'
}

minecraft {
    version = "1.7.2-10.12.0.1024"

    replaceIn "EDXProps.java"
    replace "@VERSION@", version
}

println "Building Electrodynamics version ${project.version} for Minecraft ${project.minecraft.version}"

processResources {
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        expand([
                'version': project.version,
                'mcversion': project.minecraft.version,
                'name': project.archivesBaseName,
                'modId': project.modId
        ])
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}