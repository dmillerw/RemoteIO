buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

apply plugin: 'forge'

ext.configFile = file "build.properties"

configFile.withReader {
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}

version = config.mod_version
group = "dmillerw.remoteio"
archivesBaseName = "RemoteIO"

minecraft {
    version = config.minecraft_version + "-" + config.forge_version
    replaceIn "src/main/java/dmillerw/remoteio/lib/ModInfo.java"
    replace "%MOD_VERSION%", config.mod_version
    replace "%FORGE_VERSION%", config.forge_version
}

version = "${config.minecraft_version}-${config.mod_version}"
if (System.getenv("BUILD_NUMBER") != null) {
    version += "-b" + System.getenv("BUILD_NUMBER");
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        include '**/*.info'
        include '**/*.properties'

        expand ([
                'modid': project.archivesBaseName,
                'mod_version': project.config.mod_version,
                'minecraft_version': project.config.minecraft_version,
                'build_number': project.config.build_number,
        ])
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude '**/*.info'
        exclude '**/*.properties'
    }
}

jar {
    classifier = 'universal'
}